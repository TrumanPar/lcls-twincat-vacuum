<?xml version="1.0" encoding="utf-8"?>
<TcPlcObject Version="1.1.0.1" ProductVersion="3.1.4022.18">
  <POU Name="FB_PIP_Gamma" Id="{c735b214-fb8d-491c-9d35-e2f8609cfbf0}" SpecialFunc="None">
    <Declaration><![CDATA[(* This function block does basic controls FOR the ION pump connected to a Gamma QPCe controller.
 Provides interlocking interface. Enable HV only when interlock gauge press is less than 1.0E-4 Torr *)
FUNCTION_BLOCK FB_PIP_Gamma Extends FB_Pump
VAR_INPUT
	i_stGauge	:	ST_VG; //Ion or Pirani gauge for pump interlock 
END_VAR
VAR_OUTPUT
	{attribute 'pytmc' := '
    pv: 
	'}
	stPump	:	ST_PIP; //Gamma Ion pump structure
	q_IG	:	ST_VG; //When ion pump is used as a measuring device for interlocking gate valves
END_VAR
VAR_IN_OUT
END_VAR
VAR
 rPRESS : REAL;
 iOffset: INT:=11;
 rV : REAL;
 rHVEna_SP : REAL :=1E-3; // Default protection setpoint as per the gamma QPCe manual
 (* IO Controls *)
 q_xHVEna_DO AT %Q*: BOOL;	// Enable High Voltage when TRUE
 i_iPRESS  AT %I*: INT;  //
 i_xSP_DI  AT %I* : BOOL; // NO contact //function of relay set on the QPC to HV output state

// For logging
fbLogger : FB_LogMessage := (eSubsystem:=E_SubSystem.VACUUM);
tAction : F_TRIG; // Primary action of this device (OPN_DO, PUMP_RUN, etc.)
tTimeOutAction : F_TRIG;
tOverrideActivated : R_TRIG;

tPumpStartTimeout : TON := (PT:=T#10s); // Timeout pump start if pressure < 1E-11 for more than 10s.
MinPressure : REAL:= 1E-11; // Minimum readback pressure, pump must register pressure above this to be considered running
 
END_VAR
]]></Declaration>
    <Implementation>
      <ST><![CDATA[(* Does Gamma Ion pump HV interlock. Enable HV only when interlock gauge press is less than 1.0E-4 Torr *)

(*Ensures the set point is not higher than 1.0E-4*)
IF (stPump.rHVEna_SP > rHVEna_SP) THEN
	stPump.rHVEna_SP := rHVEna_SP;
END_IF

(* Interlock *)
//stPump.xHV_ExtIlk  := i_stGauge.xPRESS_OK AND (i_stGauge.rPRESS <= stPump.rHVEna_SP);//AND NOT tPumpStartTimeout.Q); //ADP

(* Enable HV *)
IF i_stGauge.rPRESS <= stPump.rHVEna_SP AND i_stGauge.xPRESS_OK THEN
	stPump.q_xHVEna_DO := stPump.xHVEna_SW; 
	stPump.xILKOk:= TRUE;
ELSIF stPump.q_xHVEna_DO THEN 
	IF i_stGauge.rPRESS > (stPump.rHVEna_SP + stPump.rHYS_PR) THEN
		stPump.q_xHVEna_DO := FALSE;
		 stPump.xHVEna_SW := FALSE;
		 stPump.xILKOk := FALSE;
	ELSE
		stPump.q_xHVEna_DO := stPump.xHVEna_SW; 
	END_IF
ELSE
	stPump.q_xHVEna_DO  := FALSE;
	 stPump.xHVEna_SW := FALSE;
	stPump.xILKOk := FALSE;
END_IF


//stPump.q_xHVEna_DO := stPump.xHVEna_SW AND stPump.xHV_ExtIlk;
//stPump.xHVEna_SW := stPump.q_xHVEna_DO;



// Log pump turned off from loss of interlock
tAction(CLK:= stPump.xILKOk);
IF tAction.Q AND ((stPump.eState = pumpRUNNING) OR (stPump.eState = pumpSTARTING)) THEN fbLogger(sMsg:='Pump turned off due to loss of interlock.', eSevr:=TcEventSeverity.Critical); END_IF
// TAW Experimental ion pump timeout
tPumpStartTimeout(IN:=(NOT stPump.i_xHV_DI AND stPump.q_xHVEna_DO));
tTimeOutAction(CLK:=tPumpStartTimeout.Q);
IF tTimeOutAction.Q THEN fbLogger(sMsg:='Pump time out.', eSevr:=TcEventSeverity.Critical); END_IF



(*Pump STATE*)
IF tPumpStartTimeout.Q THEN //Pump timeout
	stPump.eState := pumpFAULT;
ELSIF stPump.q_xHVEna_DO AND NOT stPump.i_xHV_DI THEN
	stPump.eState := pumpSTARTING;
ELSIF NOT stPump.q_xHVEna_DO AND NOT stPump.i_xHV_DI THEN
	stPump.eState := pumpSTOPPED;
ELSIF stPump.q_xHVEna_DO AND stPump.i_xHV_DI THEN
	stPump.eState := pumpRUNNING;
ELSIF NOT stPump.q_xHVEna_DO AND stPump.i_xHV_DI THEN
	stPump.eState := pumpSTOPPING;
ELSE 
	stPump.eState := pumpFAULT;
END_IF



(*Update output gauge values*)
ACT_SetGauge();
(*Soft IO Mapping*)
IO();

// Log States and triggers
ACT_Logger();]]></ST>
    </Implementation>
    <Action Name="ACT_Logger" Id="{dd00b334-c786-42ee-ae98-6142e7f8ba70}">
      <Implementation>
        <ST><![CDATA[//STATE Logger  

IF ePrevState <> stPump.eState THEN
	  CASE stPump.eState OF
		pumpFAULT:
			fbLogger(sMsg:='Pump Fault.', eSevr:=TcEventSeverity.Critical);
  		pumpSTOPPED:
			fbLogger(sMsg:='Pump stopped.', eSevr:=TcEventSeverity.Critical);
		pumpSTARTING:
			fbLogger(sMsg:='Pump stopped.', eSevr:=TcEventSeverity.Info);
		pumpRUNNING:
			fbLogger(sMsg:='Pump running.', eSevr:=TcEventSeverity.Info);
	  END_CASE
	  ePrevState := stPump.eState;  
  END_IF
  
// ILK logger

IF NOT stPump.xILKOk AND stPump.q_xHVEna_DO THEN
 		fbLogger(sMsg:='Lost external interlock while pump was running.', eSevr:=TcEventSeverity.Critical);  	
END_IF


// Log Action
tAction(CLK:=  stPump.q_xHVEna_DO);
IF tAction.Q THEN fbLogger(sMsg:='Pump commanded to start', eSevr:=TcEventSeverity.Info); END_IF


// Log FAULT
tFault(CLK:=  NOT tPumpStartTimeout.Q);
IF tFault.Q THEN fbLogger(sMsg:='Pump Fault', eSevr:=TcEventSeverity.Critical); END_IF

]]></ST>
      </Implementation>
    </Action>
    <Action Name="ACT_SetGauge" Id="{7f72f23c-b05a-4919-b11f-c886c11a10b4}">
      <Implementation>
        <ST><![CDATA[(*MG*)
(* convert the analog input into readable pressure*)
(* use logarithmic Pressure calculation, Output= Normal and Offset = 0*)
rV := 10*INT_TO_REAL(i_iPress)/32767;
rPRESS := LREAL_TO_REAL(EXPT(10,(rV-iOffset))); (*offset = 11*)
	q_IG.rPRESS := rPRESS;
	stPump.rPRESS := rPRESS;
IF (stPump.i_xHV_DI) AND (q_IG.rPRESS < q_IG.rVAC_SP) THEN //ADP// AND (q_IG.rPRESS<>0) AND stPump.q_xHVEna_DO) THEN 
	q_IG.xPRESS_OK := TRUE;
	ELSE
	q_IG.xPRESS_OK := FALSE;
END_IF
]]></ST>
      </Implementation>
    </Action>
    <Action Name="IO" Id="{a9e5b5c0-3557-4ce9-a1b9-765648e955e7}">
      <Implementation>
        <ST><![CDATA[
(*inputs*)
stPump.i_iPRESS:=	i_iPRESS;  
stPump.i_xHV_DI:=	i_xSP_DI;
(*output*)
q_xHVEna_DO:=  stPump.q_xHVEna_DO; //According to Manual pin should be grounded to activate function]]></ST>
      </Implementation>
    </Action>
    <Method Name="M_Run" Id="{55fab705-c400-4b7f-9779-42233f752de0}">
      <Declaration><![CDATA[
METHOD PUBLIC M_Run : BOOL
VAR_INPUT
	run:bool; // set to true to run, false to stop;
END_VAR
]]></Declaration>
      <Implementation>
        <ST><![CDATA[
this^.stPump.xHVEna_SW := run;]]></ST>
      </Implementation>
    </Method>
    <LineIds Name="FB_PIP_Gamma">
      <LineId Id="3" Count="8" />
      <LineId Id="82" Count="1" />
      <LineId Id="89" Count="6" />
      <LineId Id="103" Count="0" />
      <LineId Id="106" Count="2" />
      <LineId Id="97" Count="3" />
      <LineId Id="84" Count="1" />
      <LineId Id="101" Count="1" />
      <LineId Id="86" Count="2" />
      <LineId Id="12" Count="3" />
      <LineId Id="81" Count="0" />
      <LineId Id="80" Count="0" />
      <LineId Id="77" Count="0" />
      <LineId Id="79" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="19" Count="0" />
      <LineId Id="59" Count="0" />
      <LineId Id="62" Count="2" />
      <LineId Id="76" Count="0" />
      <LineId Id="75" Count="0" />
      <LineId Id="65" Count="1" />
      <LineId Id="69" Count="1" />
      <LineId Id="104" Count="1" />
      <LineId Id="71" Count="1" />
      <LineId Id="60" Count="1" />
      <LineId Id="73" Count="0" />
      <LineId Id="20" Count="4" />
      <LineId Id="144" Count="1" />
      <LineId Id="2" Count="0" />
    </LineIds>
    <LineIds Name="FB_PIP_Gamma.ACT_Logger">
      <LineId Id="1" Count="0" />
      <LineId Id="21" Count="0" />
      <LineId Id="20" Count="0" />
      <LineId Id="2" Count="0" />
      <LineId Id="4" Count="0" />
      <LineId Id="13" Count="0" />
      <LineId Id="8" Count="0" />
      <LineId Id="15" Count="0" />
      <LineId Id="9" Count="0" />
      <LineId Id="16" Count="0" />
      <LineId Id="10" Count="0" />
      <LineId Id="17" Count="0" />
      <LineId Id="5" Count="1" />
      <LineId Id="3" Count="0" />
      <LineId Id="18" Count="1" />
      <LineId Id="23" Count="1" />
      <LineId Id="27" Count="4" />
      <LineId Id="33" Count="2" />
      <LineId Id="47" Count="4" />
      <LineId Id="22" Count="0" />
    </LineIds>
    <LineIds Name="FB_PIP_Gamma.ACT_SetGauge">
      <LineId Id="2" Count="11" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_PIP_Gamma.IO">
      <LineId Id="2" Count="4" />
      <LineId Id="1" Count="0" />
    </LineIds>
    <LineIds Name="FB_PIP_Gamma.M_Run">
      <LineId Id="3" Count="0" />
      <LineId Id="2" Count="0" />
    </LineIds>
  </POU>
</TcPlcObject>